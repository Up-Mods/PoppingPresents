plugins {
    id "multiloader-common"
}

configurations {
    commonJava {
        canBeResolved = true
    }

    commonResources {
        canBeResolved = true
    }
}

dependencies {
    compileOnly(project(":${rootProject.name}-Common"))
    commonJava project(path: ":${rootProject.name}-Common", configuration: "commonJava")
    commonResources project(path: ":${rootProject.name}-Common", configuration: "commonResources")
}

tasks.named("compileJava", JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

tasks.named("javadoc", Javadoc).configure {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

tasks.named("sourcesJar", Jar) {
    dependsOn(configurations.commonJava)
    from(configurations.commonJava)
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}
