plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
}

boolean noTag = !providers.environmentVariable("TAG").getOrNull()
boolean isPreviewBuild = providers.environmentVariable("TAG").getOrNull().with { !it || it.matches(".+-.+") }
def buildTime = providers.environmentVariable("BUILD_TIME").orElse(project.provider { new Date().format("yyyy-MM-dd'T'HH:mm:ssZ", TimeZone.getTimeZone("UTC")) })
version = providers.environmentVariable("TAG").getOrElse("${minecraft_version}-development") + (
    (isPreviewBuild && noTag) ? providers.environmentVariable("BUILD_NUMBER").map { "+build.$it".toString() }.getOrElse("") : ""
)

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(java_version)
        vendor = JvmVendorSpec.MICROSOFT
    }
    withSourcesJar()
//    withJavadocJar()
}

repositories {
    mavenCentral()
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/repository/maven-public"
    }

    maven {
        name = "NeoForge"
        url = "https://maven.neoforged.net/releases/"
    }

    maven {
        name = "Team Resourceful Maven"
        url = "https://maven.teamresourceful.com/repository/maven-releases"
    }

    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases"
    }

    maven {
        name = "Up-Mods"
        url = "https://maven.uuid.gg/releases"
    }

    maven {
        name = "Minecraft Forge (Used for Terrablender)"
        url = "https://maven.minecraftforge.net"
    }

    maven {
        name = "SmartBrainLib (SBL) Maven Repo"
        url = "https://dl.cloudsmith.io/public/tslat/sbl/maven"
    }

    maven {
        name = "GeckoLib"
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven"
        content {
            includeGroup("software.bernie.geckolib")
        }
    }

    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    compileOnly("com.google.auto.service:auto-service-annotations:1.1.1")
    annotationProcessor("com.google.auto.service:auto-service:1.1.1")

    compileOnlyApi("org.jetbrains:annotations:26.0.2-1")
    compileOnlyApi("org.jspecify:jspecify:1.0.0")
}

// Declare capabilities on the outgoing configurations.
// Read more about capabilities here: https://docs.gradle.org/current/userguide/component_capabilities.html#sec:declaring-additional-capabilities-for-a-local-component
["apiElements", "runtimeElements", "sourcesElements", /*"javadocElements"*/].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:${project.name}:$version")
        capability("$group:${project.name}-${minecraft_version}:$version")
        capability("$group:${rootProject.name}:$version")
    }
    publishing.publications.configureEach {
        suppressPomMetadataWarningsFor(variant)
    }
}

sourcesJar {
    def mod_id = project.mod_id
    from(rootProject.file("LICENSE.md")) {
        rename { "LICENSE_${mod_id}.md" }
    }
}

jar {
    def mod_id = project.mod_id
    from(rootProject.file("LICENSE.md")) {
        rename { "LICENSE_${mod_id}.md" }
    }

    manifest {
        attributes([
            "Specification-Title"         : project.mod_name,
            "Specification-Version"       : project.provider { project.version },
            "Implementation-Title"        : project.name,
            "Implementation-Version"      : project.provider { project.version },
            "Implementation-Timestamp"    : buildTime,
            "Built-On-Java"               : "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})",
            "Built-On-Minecraft"          : project.minecraft_version
        ])
    }
}

processResources {
    var expandProperties = [
        "version"                             : version,
        "group"                               : project.group,
        "mod_name"                            : project.mod_name,
        "mod_id"                              : project.mod_id,
        "credits"                             : project.credits,
        "description"                         : project.description,

        "homepage_url"                         : project.homepage_url,
        "discord_url"                             : project.discord_url,
        "sources_url"                          : project.sources_url,
        "issues_url"                          : project.issues_url,
        "license_url"                         : project.license_url,
        "modrinth_url"                        : "https://modrinth.com/mod/${project.modrinth_id}",
        "curseforge_url"                      : "https://mods.cf/${project.curseforge_id}",

        "java_version"                        : project.java_version,
        "minecraft_version"                   : project.minecraft_version,
        "curseforge_id"                       : project.curseforge_id,
        "modrinth_id"                         : project.modrinth_id,

        "neoforge_version"                    : project.neoforge_version,

        "fabric_version"                      : project.fabric_version,
        "fabric_loader_version"               : project.fabric_loader_version,

        "smart_brain_lib_neoforge_version"    : project.smart_brain_lib_neoforge_version,
        "smart_brain_lib_fabric_version"      : project.smart_brain_lib_fabric_version,

        "geckolib_neoforge_version"           : project.geckolib_neoforge_version,
        "geckolib_fabric_version"             : project.geckolib_fabric_version,

        "resourceful_config_neoforge_version" : project.resourceful_config_neoforge_version,
        "resourceful_config_fabric_version"   : project.resourceful_config_fabric_version,

        "terrablender_neoforge_version"       : project.terrablender_neoforge_version,
        "terrablender_fabric_version"         : project.terrablender_fabric_version
    ]

    var jsonExpandProperties = expandProperties.collectEntries {
        key, value -> [(key): value instanceof String ? value.replace("\n", "\\\\n") : value]
    }

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand expandProperties
    }

    filesMatching(['pack.mcmeta', 'fabric.mod.json', '*.mixins.json']) {
        expand jsonExpandProperties
    }

    inputs.properties(expandProperties)
}

publishing {
    publications {
        "mavenJava${project.name}"(MavenPublication) {
            from components.java
        }
    }
    repositories {
        if (providers.environmentVariable("MAVEN_UPLOAD_URL").isPresent()) {
            maven {
                url = providers.environmentVariable("MAVEN_UPLOAD_URL").get()
                credentials {
                    username = providers.environmentVariable("MAVEN_UPLOAD_USERNAME").get()
                    password = providers.environmentVariable("MAVEN_UPLOAD_PASSWORD").get()
                }
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(java_version.toInteger())
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
