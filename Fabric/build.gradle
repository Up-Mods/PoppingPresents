plugins {
    id "multiloader-loader"
    id "fabric-loom"
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}+${minecraft_version}"

    // Dependencies we use for this mod to run as we expect it to.
    modImplementation "net.tslat.smartbrainlib:SmartBrainLib-fabric-${minecraft_version}:${smart_brain_lib_fabric_version}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_fabric_version}"
    modImplementation "com.teamresourceful.resourcefulconfig:resourcefulconfig-fabric-${minecraft_version}:${resourceful_config_fabric_version}"
    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${minecraft_version}-${terrablender_fabric_version}"
    // Optional
    modImplementation "maven.modrinth:jade:${jade_fabric_version}+fabric"
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
}

loom {
    def aw = project(":${project.rootProject.name}-Common").file("src/main/resources/META-INF/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")

            if (project.hasProperty("mc_uuid")) {
                programArgs("--uuid", project.mc_uuid)
            }

            if (project.hasProperty("mc_username")) {
                programArgs("--username", project.mc_username)
            }
        }
        client2 {
            client()
            setConfigName("Fabric Client 2")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}
