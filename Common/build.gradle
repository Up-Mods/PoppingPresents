plugins {
    id "multiloader-common"
    id "net.neoforged.moddev"
}

neoForge {
    neoFormVersion = neo_form_version
    // If they exist, access transformers are automatically detected now.
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly "org.spongepowered:mixin:0.8.5"
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly "io.github.llamalad7:mixinextras-common:0.3.5"
    annotationProcessor "io.github.llamalad7:mixinextras-common:0.3.5"

    // Dependencies we use for this mod to run as we expect it to.
    implementation "net.tslat.smartbrainlib:SmartBrainLib-common-${minecraft_version}:${smart_brain_lib_common_version}"
    implementation "software.bernie.geckolib:geckolib-common-${minecraft_version}:${geckolib_common_version}"
    implementation "com.teamresourceful.resourcefulconfig:resourcefulconfig-common-${minecraft_version}:${resourceful_config_common_version}"
    implementation "com.github.glitchfiend:TerraBlender-common:${minecraft_version}-${terrablender_common_version}"
}

sourceSets.main.resources {
    srcDir "src/generated/resources"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    for (def dir : sourceSets.main.resources.sourceDirectories.files) {
        commonResources dir
    }
}
